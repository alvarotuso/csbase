use crate::engine::asl;

grammar;

pub Query: asl::Query = {
    "SELECT" <columns: Comma<Identifier>> "FROM" <table: Identifier> =>
        asl::Query::Select(asl::SelectQuery {table, columns, condition: Option::None}),
    "SELECT" <columns: Comma<Identifier>> "FROM" <table: Identifier> "WHERE" <condition: LogicExpression> =>
        asl::Query::Select(asl::SelectQuery {table, columns, condition: Option::Some(condition)}),
    "INSERT INTO" <table: Identifier> "("<columns: Comma<Identifier>>") VALUES ("<values: Comma<Expression>>")" =>
        asl::Query::Insert(asl::InsertQuery {table, columns, values}),
    "CREATE TABLE" <table: Identifier> "("<columns: Comma<ColumnDefinition>>")" =>
        asl::Query::CreateTable(asl::CreateTableQuery {table, columns}),
    "DROP TABLE" <table: Identifier> =>
        asl::Query::DropTable(asl::DropTableQuery {table}),
};

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

ColumnDefinition: asl::Column = {
    <i: Identifier> <t: Type> => asl::Column {name: i, column_type: t},
};

Identifier: String = {
    <i: r"[A-Za-z][A-Za-z0-9_]*"> => String::from(i),
};

LogicExpression: Box<asl::LogicExpression> = {
    LogicExpression LogicOperator Comparison => Box::new(asl::LogicExpression::LogicExpression(<>)),
    Comparison
};

Comparison: Box<asl::LogicExpression> = {
    Expression Comparator Expression => Box::new(asl::LogicExpression::Comparison(<>))
};


// Expression codification. Operators are divided into expressions and factors to encode multiplication
// and division precedence. The same logic can be applied for boolean precedence.
Expression: Box<asl::Expression> = {
    Expression ExpressionOperator Factor => Box::new(asl::Expression::Op(<>)),
    Factor
};

Factor: Box<asl::Expression> = {
    Factor FactorOperator Term => Box::new(asl::Expression::Op(<>)),
    Term,
};

Term: Box<asl::Expression> = {
    Identifier => Box::new(asl::Expression::Identifier(<>)),
    Value => Box::new(asl::Expression::Value(<>)),
    "("<Expression>")",
};

Value: asl::Value = {
    <v: r"'[^']+'"> => asl::Value::Str(String::from(v)),
    <v: r"[0-9]+"> => asl::Value::Int(v.parse().unwrap()),
    <v: r"[0-9]+\.[0-9]+"> => asl::Value::Float(v.parse().unwrap()),
    "true" => asl::Value::Bool(true),
    "false" => asl::Value::Bool(false),
};

Type: asl::Type = {
    "STRING" => asl::Type::Str,
    "INT" => asl::Type::Int,
    "FLOAT" => asl::Type::Float,
    "BOOL" => asl::Type::Bool,
}

ExpressionOperator: asl::Operator = {
    "+" => asl::Operator::Add,
    "-" => asl::Operator::Subtract,
}

FactorOperator: asl::Operator = {
    "*" => asl::Operator::Multiply,
    "/" => asl::Operator::Divide,
}

Comparator: asl::Comparator = {
    "=" => asl::Comparator::Eq,
    "!=" => asl::Comparator::Neq,
    ">" => asl::Comparator::Gt,
    ">=" => asl::Comparator::Gte,
    "<" => asl::Comparator::Lt,
    "<=" => asl::Comparator::Lte,
}

LogicOperator: asl::LogicOperator = {
    "AND" => asl::LogicOperator::And,
    "OR" => asl::LogicOperator::Or,
}